/*
 * Master Gradle initialization script
 *
 * Depends on bnd_* values from gradle.properties.
 */

import aQute.bnd.osgi.Constants

/* Add bnd gradle plugin as a script dependency */
buildscript {
  repositories {
    maven {
      url uri(bnd_repourl)
    }
    mavenCentral()
  }
  dependencies {
    classpath bnd_plugin
    classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.4.6'
  }
  /* Add bnd gradle plugin to buildscript classpath of rootProject */
  def bndPlugin = files(configurations.classpath.files)
  gradle.rootProject {
    buildscript {
      dependencies {
        classpath bndPlugin
      }
    }
  }
}

gradle.ext.bndWorkspaceConfigure = { workspace ->
  workspace.setProperty('bnd_repourl', uri(bnd_repourl).toString())
  /*
   * Compute the build time stamp. 
   * If the git workspace is clean, the build time is the time of the head commit.
   * If the git workspace is dirty, the build time is the current time.
   */
  if ('git diff --no-ext-diff --quiet'.execute().waitFor() == 0) {
    workspace.setProperty(Constants.TSTAMP, 'git show --no-patch --format=%ct000'.execute().text.trim())
  } else {
    workspace.setProperty(Constants.TSTAMP, Long.toString(System.currentTimeMillis()))
  }
}

apply plugin: 'biz.aQute.bnd.workspace'
