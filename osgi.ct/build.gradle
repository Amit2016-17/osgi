/*
 * osgi.ct Gradle build script
 */

ext.riBuildDir = rootProject.file("osgi.ri/${bnd('target-dir')}")

tasks.addRule('Pattern: ct.<name>: Run the CT for <name>.') { taskName ->
  if (taskName.startsWith('ct.')) {
    task(taskName) {
      ext.book = taskName - 'ct.'
      description "Run the CT for ${book}."
      group 'verification'
      ext.destinationDir = new File(buildDir, "osgi.ct.${book}")
      inputs.file new File(riBuildDir, 'osgi.ri.core.jar')
      inputs.file new File(riBuildDir, "osgi.ri.${book}.jar")
      inputs.file new File(buildDir, "osgi.ct.${book}.jar")
      doFirst {
        project.delete(destinationDir)
        project.mkdir(destinationDir)
      }
      doLast {
        inputs.files.each { jar ->
          copy {
            from zipTree(jar)
            into destinationDir
          }
        }
        javaexec {
          workingDir destinationDir
          main = '-jar' // next arg must be the bnd jar
          args new File(destinationDir, 'jar/bnd.jar')
          args '--exceptions'
          args 'runtests'
          args '--title', "osgi.ct.${book}"
        }.assertNormalExitValue()
      }
    }
  }
}

task('ct') {
  description 'Run all the CTs.'
  group 'verification'
  dependsOn 'ct.core', 'ct.cmpn', 'ct.enterprise', 'ct.residential'
}
