/*
 * osgi.ct Gradle build script
 */

ext.riBuildDir = rootProject.file("osgi.ri/${bnd('target-dir')}")

tasks.addRule('Pattern: prepct.<name>: Prepare the CT for <name>.') { taskName ->
  if (taskName.startsWith('prepct.')) {
    task(taskName) {
      ext.book = taskName - 'prepct.'
      description "Prepare the CT for ${book}."
      group 'verification'
      ext.destinationDir = new File(buildDir, "osgi.ct.${book}")
      inputs.files fileTree(riBuildDir).include('osgi.ri.core*.jar', "osgi.ri.${book}*.jar")
      inputs.files fileTree(buildDir).include("osgi.ct.${book}*.jar")
      doFirst {
        project.delete(destinationDir)
        project.mkdir(destinationDir)
      }
      doLast {
        inputs.files.each { jar ->
          copy {
            from zipTree(jar)
            into destinationDir
          }
        }
      }
    }
  }
}

tasks.addRule('Pattern: ct.<name>: Run the CT for <name>.') { taskName ->
  if (taskName.startsWith('ct.')) {
    task(taskName) {
      ext.book = taskName - 'ct.'
      description "Run the CT for ${book}."
      group 'verification'
      ext.destinationDir = new File(buildDir, "osgi.ct.${book}")
      dependsOn "prepct.${book}"
      doLast {
        javaexec {
          workingDir destinationDir
          main = '-jar' // first arg must be the bnd jar
          args new File(destinationDir, 'jar/bnd.jar')
          args '--exceptions'
          args 'runtests'
          args '--title', "osgi.ct.${book}"
        }.assertNormalExitValue()
      }
    }
  }
}

task('ct') {
  description 'Run all the CTs.'
  group 'verification'
  dependsOn 'ct.core', 'ct.cmpn', 'ct.enterprise', 'ct.residential'
}
