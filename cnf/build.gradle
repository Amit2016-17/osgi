/*
 * cnf Gradle build script
 */
def cnfProject = project

/* After building, we need to refresh the bnd Projects to pickup the bnd plugins built in cnf. */
task('refresh') {
  dependsOn classes
  onlyIf {
    compileJava.didWork || processResources.didWork
  }
  doLast {
    logger.info "Refresh bnd Projects after building bnd plugins in ${project.name}"
    bnd.project.refresh()
    rootProject.subprojects {
      if (project != cnfProject) {
        bnd.project.refresh()
      }
    }
  }
}

jar {
  dependsOn refresh
}

clean {
  /* clean bnd cache if not offline */
  enabled !bndWorkspace.isOffline()
  doLast {
    project.delete(file('cache'))
  }
}

/* bnd plugins influence bundle contents, so add cnf output to jar inputs */
def cnfOutput = sourceSets.main.output.files.collect {
  fileTree(it)
}
rootProject.subprojects {
  if (project != cnfProject) {
    jar {
      inputs.files cnfOutput
      dependsOn ":${cnfProject.name}:assemble"
    }
  }
}
