/*
 * OSGi companion code Gradle script
 */

group bnd('groupid')
version bnd('spec.version', bnd('osgi.version'))

ext.javadocSpecs = bnd("${project.name}.specs", project.name)
ext.javadocTitle = bnd('javadoc.title', project.name)

javadoc {
  source bnd.allSrcDirs
  javadocSpecs.split(/\s*,\s*/).each {
    include it.replace('.','/')+'/**/*.java'
  }
  configure(options) {
    docTitle = "OSGi&trade; ${javadocTitle} Release ${bnd.osgi_release}"
    windowTitle = "OSGi ${javadocTitle} Release ${bnd.osgi_release}"
    bottom = "<font size=-1>${bnd.copyright_html} Licensed under the <a href=\"{@docRoot}/LICENSE.html\">OSGi Specification License, Version 2.0</a></font>"
    header = "<b>OSGi&trade; ${javadocTitle}</b><br/><font size=-1>Release ${bnd.osgi_release}</font>"
    memberLevel = JavadocMemberLevel.PROTECTED
    charSet = 'UTF-8'
    encoding = 'UTF-8'
    docEncoding = 'UTF-8'
    version = false
    use = true
    splitIndex = true
    author = false
    noDeprecated = false
    noDeprecatedList = false
    noIndex = false
    noNavBar = false
    noTree = false
    tags = ['Immutable:t:"Immutable"',
            'ThreadSafe:t:"ThreadSafe"',
            'NotThreadSafe:t:"NotThreadSafe"',
            'GuardedBy:mf:"Guarded By:"',
            'security:m:"Required Permissions"',
            'noimplement:t:"Consumers of this API must not implement this interface"']
    classpath bnd.project.getBootclasspath()*.getFile()
    links('http://docs.oracle.com/javase/6/docs/api/')
  }
  if (project.name.startsWith('osgi.')) {
    // compute dependencies on other javadoc builds based upon the classpath
    configurations.compile.each {
      def entry = rootProject.relativePath(it).replace(File.separatorChar, '/' as char)
      def matcher = entry =~ /^(osgi\.([^\/]*))/
      if (matcher) {
        dependsOn ":${matcher[0][1]}:javadoc"
        options.linksOffline("../${matcher[0][2]}", rootProject.file("${matcher[0][1]}/${relativePath(destinationDir)}").absolutePath)
      }
    }
  }
  /* Turn off javadoc 8 overly pedantic lint checking */
  if (JavaVersion.current().isJava8Compatible()) {
    options.addStringOption('Xdoclint:none', '-quiet') 
  }
  doFirst {
    project.delete(destinationDir)
    logger.info "Title         : ${options.windowTitle}"
    logger.info "Packages      : ${javadocSpecs}"
    logger.info "Destdir       : ${destinationDir}"
  }
  def legal = rootProject.file('osgi.companion/legal/javadoc')
  inputs.dir legal
  doLast {
    copy {
      from legal
      into destinationDir
    }
  }
}

task(type: Jar, 'javadocJar') {
  description 'Jar the javadoc.'
  group 'documentation'
  dependsOn javadoc
  from javadoc.destinationDir
  classifier 'javadoc'
}

task(type: Jar, 'sourcesJar') {
  description 'Jar the sources.'
  group 'documentation'
  dependsOn jar
  inputs.file jar.archivePath
  from zipTree(jar.archivePath).matching {
    include 'OSGI-OPT/src/**'
    include '*'
  }
  eachFile { FileCopyDetails fcp ->
    fcp.path = fcp.path - 'OSGI-OPT/src/'
  }
  includeEmptyDirs = false
  classifier = 'sources'
}

task('pom') {
  description 'Generate the pom file.'
  group 'documentation'
  dependsOn jar
  def pomname = "${archivesBaseName}-${version}.pom"
  ext.pomfile = new File(buildDir, pomname)
  inputs.file jar.archivePath
  outputs.file pomfile
  doLast {
    copy {
      from zipTree(jar.archivePath).matching {
        include "META-INF/maven/${project.group}/${archivesBaseName}/pom.xml"
      }
      eachFile { FileCopyDetails fcp ->
        fcp.path = pomname
      }
      into buildDir
      includeEmptyDirs = false
    }
  } 
}

artifacts {
  archives sourcesJar
  archives javadocJar
  archives(pom.pomfile) {
    builtBy pom
  }
}
